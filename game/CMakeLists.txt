project(game)
set(CMAKE_CXX_STANDARD 20)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/cmake)

file(GLOB_RECURSE SOURCE_FILES CONFIGURE_DEPENDS src/*.cpp src/*.hpp)
source_group(TREE ${CMAKE_CURRENT_LIST_DIR}/src FILES ${SOURCE_FILES})

find_package(pandora REQUIRED)
add_executable(game ${SOURCE_FILES})

target_include_directories(
    game PRIVATE 
    src/
    ${PANDORA_INCLUDE_DIRS}
)

target_link_directories(game PRIVATE ${PANDORA_LIBRARY_DIRS})
target_link_libraries(game PRIVATE pandora)

if(TARGET_PLATFORM_NATIVE)
    add_custom_command(TARGET game POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:game> ${CMAKE_CURRENT_LIST_DIR}/bin
    )
elseif(TARGET_PLATFORM_WEB)
    target_link_options(game PRIVATE
        "--shell-file=${CMAKE_CURRENT_SOURCE_DIR}/src/emscripten/shell.html"
    )

    # Generate asset manifest using forge tool
    set(FORGE_EXECUTABLE "${CMAKE_SOURCE_DIR}/pandora/tools/forge/bin/forge.exe")
    set(MANIFEST_OUTPUT "${CMAKE_CURRENT_LIST_DIR}/bin/manifest.json")

    add_custom_command(
        OUTPUT "${MANIFEST_OUTPUT}"
        COMMAND "${FORGE_EXECUTABLE}" manifest
        WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
        COMMENT "Generating asset manifest..."
        DEPENDS "${FORGE_EXECUTABLE}"
    )

    add_custom_target(generate_manifest DEPENDS "${MANIFEST_OUTPUT}")
    add_dependencies(game generate_manifest)

    set_target_properties(game PROPERTIES
        SUFFIX ".html"
        LINK_DEPENDS "${MANIFEST_OUTPUT}"
    )
    add_custom_command(TARGET game POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE_DIR:game>/$<TARGET_FILE_BASE_NAME:game>.html ${CMAKE_CURRENT_LIST_DIR}/bin
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE_DIR:game>/$<TARGET_FILE_BASE_NAME:game>.js ${CMAKE_CURRENT_LIST_DIR}/bin
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE_DIR:game>/$<TARGET_FILE_BASE_NAME:game>.wasm ${CMAKE_CURRENT_LIST_DIR}/bin
    )
endif()
